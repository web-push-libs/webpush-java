plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '6.1.0'

    // Used by release.gradle
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.30.0'
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

group 'nl.martijndwars'
version '5.1.2-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // For CLI
    implementation group: 'com.beust', name: 'jcommander', version: '1.78'

    // For making HTTP requests
    implementation group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.4'

    // For making async HTTP requests
    implementation group: 'org.asynchttpclient', name: 'async-http-client', version: '2.12.2'

    // For cryptographic operations
    shadow group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.68'

    // For creating and signing JWT
    implementation group: 'org.bitbucket.b_c', name: 'jose4j', version: '0.7.6'

    // For parsing JSON
    testImplementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    // For making HTTP requests
    testImplementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.13'

    // For testing, obviously
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.7.0'

    // For running JUnit tests
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.0'

    // For turning InputStream to String
    testImplementation group: 'commons-io', name: 'commons-io', version: '2.8.0'

    // For reading the demo vapid keypair from a pem file
    testImplementation group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.68'

    // For verifying Base64Encoder results in unit tests
    testImplementation group: 'com.google.guava', name: 'guava', version: '31.0.1-jre'
}

wrapper {
    gradleVersion = '5.1'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

compileTestJava {
    sourceCompatibility = 1.8
}

mainClassName = 'nl.martijndwars.webpush.cli.Cli'

run {
    classpath configurations.shadow.files
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
        showStandardStreams true
        exceptionFormat 'full'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

if (hasProperty('release')) {
    apply from: 'release.gradle'
}
