plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '5.2.0'

    // Used by release.gradle
    id 'maven-publish'
    id 'signing'
    id 'io.codearte.nexus-staging' version '0.21.1'
}

apply plugin: 'application'
apply plugin: 'com.github.johnrengelman.shadow'

group 'nl.martijndwars'
version '5.1.1-SNAPSHOT'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    // For CLI
    compile group: 'com.beust', name: 'jcommander', version: '1.78'

    // For making async HTTP requests
    compile group: 'org.apache.httpcomponents', name: 'httpasyncclient', version: '4.1.4'

    // For cryptographic operations
    shadow group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.64'

    // For creating and signing JWT
    compile group: 'org.bitbucket.b_c', name: 'jose4j', version: '0.7.0'

    // For parsing JSON
    testCompile group: 'com.google.code.gson', name: 'gson', version: '2.8.6'

    // For making HTTP requests
    testCompile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.10'

    // For testing, obviously
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.5.2'

    // For running JUnit tests
    testRuntime group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.6.0'

    // For turning InputStream to String
    testCompile group: 'commons-io', name: 'commons-io', version: '2.6'

    // For reading the demo vapid keypair from a pem file
    testCompile group: 'org.bouncycastle', name: 'bcpkix-jdk15on', version: '1.64'

    // For verifying Base64Encoder results in unit tests
    testCompile group: 'com.google.guava', name: 'guava', version: '28.2-jre'
}

wrapper {
    gradleVersion = '5.1'
}

compileJava {
    sourceCompatibility = 1.7
    targetCompatibility = 1.7
}

compileTestJava {
    sourceCompatibility = 1.8
}

mainClassName = 'nl.martijndwars.webpush.cli.Cli'

run {
    classpath configurations.shadow.files
}

test {
    useJUnitPlatform()

    testLogging {
        events 'PASSED', 'FAILED', 'SKIPPED'
        showStandardStreams true
        exceptionFormat 'full'
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

if (hasProperty('release')) {
    apply from: 'release.gradle'
}
